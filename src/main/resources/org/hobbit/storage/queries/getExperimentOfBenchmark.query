PREFIX hobbit: <http://w3id.org/hobbit/vocab#>

CONSTRUCT {
    ?e a hobbit:Experiment .
    ?e hobbit:involvesSystemInstance ?system .
    ?e hobbit:involvesBenchmark %BENCHMARK_URI% .
    ?e ?experimentProp  ?experimentObj .
    %BENCHMARK_URI% ?benchmarkProp ?benchmarkObject .
    ?system ?systemProp ?systemObject .
    ?resultProp a hobbit:KPI .
    ?resultProp ?resultPropProp ?resultPropObject .
    ?parameterProp a ?parameterType .
    ?parameterProp ?parameterPropProp ?parameterPropObject .
}
WHERE {
GRAPH %GRAPH_URI% {
    {
        SELECT ?e
        WHERE {
            ?e hobbit:involvesBenchmark %BENCHMARK_URI% .
            ?e a hobbit:Experiment .
        }
    }
    {
        ?e ?experimentProp ?experimentObj .
    }
    UNION
    {
        ?e hobbit:involvesSystemInstance ?system .
        OPTIONAL { ?system ?systemProp ?systemObject . }
    }
    UNION
    {
        OPTIONAL { %BENCHMARK_URI% ?benchmarkProp ?benchmarkObject . }
    }
    UNION
    {
        SELECT ?parameterProp ?parameterType ?parameterPropProp ?parameterPropObject WHERE {
            {
                SELECT ?parameterProp
                WHERE {
                    %BENCHMARK_URI% hobbit:hasParameter ?parameterProp .
                }
            }
            ?parameterProp a ?parameterType .
            filter(?parameterType in (hobbit:Parameter, hobbit:ConfigurableParameter, hobbit:FeatureParameter))
            OPTIONAL { ?parameterProp ?parameterPropProp ?parameterPropObject . }
        }
    }
    UNION
    {
        SELECT ?resultProp ?resultPropProp ?resultPropObject WHERE {
            {
                SELECT ?resultProp
                WHERE {
                    %BENCHMARK_URI% hobbit:measuresKPI ?resultProp .
                    ?resultProp a hobbit:KPI .
                }
            }
            OPTIONAL { ?resultProp ?resultPropProp ?resultPropObject . }
        }
    }
}}
