PREFIX hobbit: <http://w3id.org/hobbit/vocab#>

CONSTRUCT {
    %EXPERIMENT_URI% a hobbit:Experiment .
	%EXPERIMENT_URI% hobbit:involvesSystemInstance ?system .
	%EXPERIMENT_URI% hobbit:involvesBenchmark ?benchmark .
	%EXPERIMENT_URI% ?experimentProp	?experimentObj .
	%EXPERIMENT_URI% hobbit:isPartOf ?challengeTask .
	%EXPERIMENT_URI% ?resultProp ?resultValue .
	?resultProp a hobbit:KPI .
	%EXPERIMENT_URI% ?parameterProp ?parameterValue . 
	?parameterProp a ?parameterType .
}
WHERE {
  GRAPH %GRAPH_URI% {
    %EXPERIMENT_URI% a hobbit:Experiment .
	%EXPERIMENT_URI% hobbit:involvesSystemInstance ?system .
	%EXPERIMENT_URI% hobbit:involvesBenchmark ?benchmark .
	%EXPERIMENT_URI% ?experimentProp	?experimentObj .
	OPTIONAL { %EXPERIMENT_URI% hobbit:isPartOf ?challengeTask .}
	OPTIONAL {
		%EXPERIMENT_URI% ?resultProp ?resultValue .
		?benchmark hobbit:measuresKPI ?resultProp .
		?resultProp a hobbit:KPI .
	}
	OPTIONAL {
		%EXPERIMENT_URI% ?parameterProp ?parameterValue .
		?benchmark hobbit:hasParameter ?parameterProp .
		?parameterProp a ?parameterType .
		{?parameterProp a hobbit:Parameter} UNION {?parameterProp a hobbit:ConfigurableParameter} UNION {?parameterProp a hobbit:FeatureParameter}.
	}
  }
}