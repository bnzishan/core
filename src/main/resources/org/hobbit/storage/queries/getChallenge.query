PREFIX hobbit: <http://w3id.org/hobbit/vocab#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
    %CHALLENGE_URI% a hobbit:Challenge .
	%CHALLENGE_URI% ?challengeProp ?challengeObj .
	?challengeTask a hobbit:ChallengeTask .
	?challengeTask ?taskProp ?taskObj .
	?challengeTask hobbit:isTaskOf %CHALLENGE_URI% .
	?challengeTask hobbit:involvesBenchmark ?benchmark .
	?challengeTask hobbit:involvesSystemInstance ?system .
	?challengeTask ?parameterProp ?parameterValue .
	?benchmark hobbit:hasParameter ?parameterProp . 
}
WHERE {
  GRAPH %GRAPH_URI% {
    %CHALLENGE_URI% a hobbit:Challenge .
	%CHALLENGE_URI% ?challengeProp ?challengeObj .
	OPTIONAL {
		?challengeTask a hobbit:ChallengeTask .
		?challengeTask ?taskProp ?taskObj .
		?challengeTask hobbit:isTaskOf %CHALLENGE_URI% .
		?challengeTask hobbit:involvesBenchmark ?benchmark .
		OPTIONAL {
			?challengeTask hobbit:involvesSystemInstance ?system .
		}
		OPTIONAL {
			?challengeTask ?parameterProp ?parameterValue .
			?benchmark hobbit:hasParameter ?parameterProp .
			{?parameterProp a hobbit:Parameter} UNION {?parameterProp a hobbit:ConfigurableParameter} UNION {?parameterProp a hobbit:FeatureParameter}.
		}
	}
  }
}